pragma Singleton
import QtQuick
import Quickshell
import Quickshell.Services.Mpris

Singleton {
    id: root
    property var last: null
    property var live: null
    readonly property var player: live || last
    readonly property bool playing: live !== null
    readonly property string title: player ? player.trackTitle || player.identity : ""

    function pick() {
        const v = Mpris.players.values
        let found = null
        for (let i = 0; i < v.length; i++) {
            if (v[i].isPlaying) {
                found = v[i]
                break
            }
        }
        live = found
        if (found)
            last = found
        else if (last) {
            let still = false
            for (let i = 0; i < v.length; i++) {
                if (v[i] === last) {
                    still = true
                    break
                }
            }
            if (!still) last = null
        }
    }

    Connections {
        target: Mpris.players
        function onValuesChanged() { root.pick() }
    }

    Instantiator {
        model: Mpris.players
        Connections {
            required property var modelData
            target: modelData
            function onIsPlayingChanged() { root.pick() }
        }
    }

    Component.onCompleted: pick()
}
